# Config file for the pFUnit package
# It defines the following variables
#    PFUNIT_INCLUDE_DIRS - Include directories for pFUnit
#    PFUNIT_LIBRARIES    - libraries to link against
#    PFUNIT_PARSER       - Command for executing the pFUnit parsers for parsing .pf-files
#    PFUNIT_DRIVER       - The pFUnit driver needed for running tests
#    add_pfuint_test     - Helper function for defining test suites with .pf-files

find_package (PythonInterp
    REQUIRED
    )

set (PFUNIT_INCLUDE_DIRS ${CMAKE_CURRENT_LIST_DIR}/mod)
set (PFUNIT_LINK_DIRS ${CMAKE_CURRENT_LIST_DIR}/lib)
find_library (PFUNIT_LIBRARIES pfunit
    PATHS ${CMAKE_CURRENT_LIST_DIR}/lib
    NO_DEFAULT_PATH)
set (PFUNIT_PARSER "${PYTHON_EXECUTABLE}" "${CMAKE_CURRENT_LIST_DIR}/bin/pFUnitParser.py")
set (PFUNIT_DRIVER "${CMAKE_CURRENT_LIST_DIR}/include/driver.F90")


# Function     : add_pfunit_test 
#
# Description  : Helper function for compiling and adding pFUnit tests to the CTest testing framework. Any libraries needed
#                in testing should be linked to manually.
#                IMPORTANT! This function will only work if the test source filename is the same as the module inside it!
#                For example, the file testSomething.pf should contain the module testSomething.
#
# Arguments    : - test_suite_name: Name of the test suite
#                - test_sources   : List of pf-files to be compiled
#                - extra_sources  : List of extra Fortran source code used for testing (if none, input empty string "")
#                - extra_sources  : List of extra C/C++ source code used for testing (if none, input empty string "")
#
# Example usage: enable_testing()
#                set (TEST_SOURCES
#                   testMyLib.pf
#                    )
#                add_pfunit_test (myTestSuite "${TEST_SOURCES} "" "")
#                target_link_libraries (myTestSuite myLibrary) #Assuming "myLibrary" is already defined
#                
#                Compile the test suite:   make myTestSuite
#                Run the tests with CTest: ctest -R myTestSuite --verbose
function (add_pfunit_test test_suite_name test_sources extra_sources extra_sources_c)

    set (testsuite_exe_name run_${test_suite_name})

    set (SRCGEN_DIR "src_gen")
    set (TEST_DIR ${CMAKE_CURRENT_BINARY_DIR}/${test_suite_name})
    execute_process(COMMAND ${CMAKE_COMMAND} -E make_directory ${TEST_DIR}/${SRCGEN_DIR})

    set (TEST_SUITES_INC "")
    foreach (file ${test_sources})
        get_filename_component (basename ${file} NAME_WE)
        set (fsrc "${TEST_DIR}/${SRCGEN_DIR}/${basename}.F90")
        list (APPEND test_sources_f90 ${fsrc})
        set (TEST_SUITES_INC "${TEST_SUITES_INC}ADD_TEST_SUITE(${basename}_suite)\n")
    endforeach()

    file (WRITE ${TEST_DIR}/testSuites.inc ${TEST_SUITES_INC})

    list (LENGTH test_sources len)
    math(EXPR n "${len} - 1")
    foreach (i RANGE ${n})
        list (GET test_sources ${i} pf_file)
        list (GET test_sources_f90 ${i} f90_file)
        add_custom_command(
            OUTPUT ${f90_file}
            COMMAND ${PFUNIT_PARSER} ${CMAKE_CURRENT_SOURCE_DIR}/${pf_file} ${f90_file}
            MAIN_DEPENDENCY ${CMAKE_CURRENT_SOURCE_DIR}/${pf_file}
            #COMMENT "Generating '${f90_file}' from '${CMAKE_CURRENT_SOURCE_DIR}/${pf_file}'"
            )
    endforeach()

    add_executable (${testsuite_exe_name} ${test_sources_f90} ${extra_sources} ${PFUNIT_DRIVER})

    if (NOT extra_sources_c STREQUAL "")
        add_library (${test_suite_name}_c STATIC ${extra_sources_c})
        target_link_libraries (${testsuite_exe_name} ${test_suite_name}_c)
    endif ()

    target_link_libraries (${testsuite_exe_name} ${PFUNIT_LIBRARIES})
    target_include_directories (${testsuite_exe_name} PRIVATE ${PFUNIT_INCLUDE_DIRS})
    target_include_directories (${testsuite_exe_name} PRIVATE ${TEST_DIR})

    if(WIN32)
        set_property(TARGET ${test_suite_name}
            PROPERTY LINK_FLAGS " /INCREMENTAL:NO ")
    endif (WIN32)


    add_test (NAME ${test_suite_name}
        WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
        COMMAND ${testsuite_exe_name} --verbose
        )

endfunction (add_pfunit_test test_suite_name test_sources extra_sources extra_sources_c)
